version = '0.9.2'

// for integration tests
sourceSets {
    intTest {}
}
configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    compileOnly 'foundation.icon:javaee-api:0.9.2'
    implementation 'foundation.icon:javaee-scorex:0.5.2'
    implementation 'com.github.sink772:minimal-json:0.9.7'

    testImplementation 'foundation.icon:javaee-unittest:0.9.4'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    intTestImplementation 'foundation.icon:javaee-integration-test:0.9.0'
    intTestImplementation 'foundation.icon:icon-sdk:2.1.0'
    intTestRuntimeOnly project(':hello-world')
}

optimizedJar {
    mainClassName = 'com.iconloop.score.example.MultiSigWallet'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

deployJar {
    endpoints {
        lisbon {
            uri = 'https://lisbon.net.solidwallet.io/api/v3'
            nid = 0x2
        }
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 0x3
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
    parameters {
        arg('_walletOwners', 'hxe5679f118e093657b71967b0dfddb4d00cbd80b4,hx65f5d819d4b3897c39790f1f8b8a2bdcaeb605d0')
        arg('_required', '0x2')
    }
}

test {
    useJUnitPlatform()
}

def helloWorldJar = project(':hello-world').getTasks().getByName('optimizedJar')

task integrationTest(type: Test, dependsOn: optimizedJar) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    testLogging.showStandardStreams = true

    // use the common config files
    systemProperty('env.props', new File(project(':testinteg').projectDir, 'conf/env.props'))

    def prefix = 'score.path.'
    systemProperty(prefix + project.name, optimizedJar.outputJarName)
    systemProperty(prefix + 'hello-world', helloWorldJar.outputJarName)
}

integrationTest.dependsOn(helloWorldJar)
