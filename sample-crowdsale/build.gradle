version = '0.1.0'

// for integration tests
sourceSets {
    intTest {}
}
configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    compileOnly 'foundation.icon:javaee-api:0.9.2'

    testImplementation project(':irc2-token')
    testImplementation 'foundation.icon:javaee-unittest:0.9.4'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    intTestImplementation 'foundation.icon:javaee-integration-test:0.9.0'
    intTestImplementation 'foundation.icon:icon-sdk:2.1.0'
}

optimizedJar {
    mainClassName = 'com.iconloop.score.example.SampleCrowdsale'
    archivesBaseName = 'sample-crowdsale'
}

deployJar {
    endpoints {
        lisbon {
            uri = 'https://lisbon.net.solidwallet.io/api/v3'
            nid = 0x2
        }
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 0x3
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
    parameters {
        arg('_fundingGoalInIcx', '0x64')
        arg('_tokenScore', 'cx451cee702db0e48aacbe446d53cbd8aa00142354')
        arg('_durationInBlocks', '0x20')
    }
}

test {
    useJUnitPlatform()
}

def sampleTokenJar = project(':irc2-token').getTasks().getByName('optimizedJar')

task integrationTest(type: Test, dependsOn: optimizedJar) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    testLogging.showStandardStreams = true

    // use the common config files
    systemProperty('env.props', new File(project(':testinteg').projectDir, 'conf/env.props'))

    def prefix = 'score.path.'
    systemProperty(prefix + project.name, optimizedJar.outputJarName)
    systemProperty(prefix + 'sample-token', sampleTokenJar.outputJarName)
}

integrationTest.dependsOn(sampleTokenJar)
