plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

optimizedJar.enabled = false

dependencies {
    implementation 'foundation.icon:icon-sdk:2.1.0'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

def projectName = 'javaee-integration-test'
def repoUrl = 'https://github.com/icon-project/java-score-examples/tree/master/testinteg'
def snapshotSuffix = rootProject.hasProperty('release') ? '' : '-SNAPSHOT'
version = VERSION + snapshotSuffix

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "iconfoundation"
            name "icon.foundation"
            email "foo@icon.foundation"
        }
    }
    scm {
        url repoUrl
    }
}

publishing {
    repositories {
        maven {
            name = 'mavenCentral'
            def releasesUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = rootProject.hasProperty('mavenCentralUsername') ? "$mavenCentralUsername" : ''
                password = rootProject.hasProperty('mavenCentralPassword') ? "$mavenCentralPassword" : ''
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = GROUP
            artifactId = projectName
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', projectName)
                root.appendNode('description', 'An Integration Testing Framework for Java SCORE')
                root.appendNode('url', repoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

signing {
    required rootProject.hasProperty('release')
    if (rootProject.hasProperty('signingKey')) {
        def signingKey = rootProject.findProperty("signingKey")
        def signingPassword = rootProject.findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.mavenJava
}
